buildscript {
    repositories {
        mavenCentral()
    }
}
// possiblly switch to id "us.ascendtech.gwt.modern" version "0.8.15"
apply plugin: "gwt"
apply plugin: "war"

gwt {
    gwtVersion = "$gwtFrameworkVersion"
    maxHeapSize = '1G'
    minHeapSize = '1G'
    modules 'net.rptools.maptool.GdxDefinition'
    devModules 'net.rptools.maptool.GdxDefinitionSuperdev'
    project.webAppDirName = "src/webapp"
    compiler.strict = true
    compiler.disableCastChecking = true
    sourceLevel = 1.11
	compiler.style = org.wisepersist.gradle.plugins.gwt.Style.DETAILED
    superDev.noPrecompile = true
    jsInteropExports.shouldGenerate = true
    logLevel = "INFO"
}

dependencies {
    implementation "com.github.tommyettinger:gdx-backend-gwt:1.100.1"
    implementation "com.github.tommyettinger:gdx-backend-gwt:1.100.1:sources"
	implementation "com.badlogicgames.gdx:gdx:$gdxVersion:sources"
	implementation "com.crashinvaders.lml:gdx-kiwi:$lmlVersion:sources"
	implementation "com.github.MrStahlfelge.gdx-websockets:core:$websocketVersion:sources"
	implementation "com.github.MrStahlfelge.gdx-websockets:html:$websocketVersion"
	implementation "com.github.MrStahlfelge.gdx-websockets:html:$websocketVersion:sources"
    implementation 'com.google.jsinterop:jsinterop-annotations:2.0.0:sources'
    implementation 'org.slf4gwt:slf4gwt:1.4:sources'
    implementation 'com.axellience:vue-gwt:1.0.0'
    annotationProcessor 'com.axellience:vue-gwt-processors:1.0.0'
	implementation project(':core')
}

task addSource {
    doLast {
        sourceSets.main.compileClasspath += files(project(':core').sourceSets.main.allJava.srcDirs)
        sourceSets.main.compileClasspath += files(project(':shared').sourceSets.main.allJava.srcDirs)
        // for adding jre java files missing in gwt
        sourceSets.main.compileClasspath += project.files("src/main/ressources")
    }
}

tasks.compileGwt.dependsOn(addSource)
tasks.draftCompileGwt.dependsOn(addSource)
tasks.checkGwt.dependsOn(addSource)
tasks.gwtDev.dependsOn(addSource)

sourceCompatibility = JavaVersion.VERSION_11
sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'build/generated/sources/annotationProcessor/java/main'
        }
        resources {
            srcDir 'src/main/java'
        }
        output.resourcesDir = "build/classes/java/main"
    }
}
compileJava.dependsOn(processResources)
compileJava.options.compilerArgs << '-parameters'

//everything below is for lombok
configurations {
    lom
}

dependencies {
    lom "org.projectlombok:lombok:${lombokVersion}"
    implementation configurations.lom.dependencies
    annotationProcessor configurations.lom.dependencies
}

draftCompileGwt {
    doFirst {
        jvmArgs "-javaagent:${configurations.lom.asPath}=ECJ"
    }
}

compileGwt {
    doFirst {
        jvmArgs "-javaagent:${configurations.lom.asPath}=ECJ"
    }
}

gwtDev {
    doFirst {
        jvmArgs "-javaagent:${configurations.lom.asPath}=ECJ"
    }
}